#!/bin/zsh

# Script to print a document one page at a time, in case you don't
# have a duplex printer. Uses ghostscript and CUPS.

# Prints all pages by default; to specify a subset use e.g.:
# PAGES=$(echo {4..7}) page-at-a-time doc.pdf

# FHE 12 Oct 2018 change to using gs instead of page-ranges because of
# a cups-filters bug where page-ranges won't work on remote printers

IN=$1

[[ $IN = *.pdf ]] || { >&2 echo Need a PDF file!; exit 1 }

#NPAGES=$(pdfinfo $IN| grep Pages | cut -d : -f 2)
NPAGES=$(pdfinfo $IN| perl -ne '/Pages:\s*([\d]+)/ && print $1')

echo NPAGES=$NPAGES
: ${PAGES:=$(echo {1..$NPAGES})}
eval "pages=($PAGES)"
echo PAGES=$PAGES
last=$pages[-1]

SEL=$(mktemp -t page-at-a-time.XXXXXXX.pdf)

for i in $pages; do
    >&2 echo "Printing page $i";
    gs -sDEVICE=pdfwrite -dNOPAUSE -dBATCH -dFirstPage=$i -dLastPage=$i -sOutputFile=$SEL $IN
#    lp -o page-ranges=$i $IN;
    lp $SEL;
    [[ $i -lt $last ]] && read;
done
